grammar org.eclipse.january.geometry.model.STL with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/january/geometry"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Geometry returns Geometry:
	{Geometry}
	(nodes+=Shape_Impl)*
;

Shape_Impl returns Shape:
	{Shape}
	'solid'
	(name=EString)? (ID | WS | STRING | DOUBLE | ANY_OTHER)*
	(triangles+=Triangle)* WS*
	'endsolid' EString? (ID | WS | STRING | DOUBLE | ANY_OTHER)*
;

Triangle returns Triangle:
	{Triangle}
	'facet' ('normal' normal=Vertex)
		'outer' 'loop'
			('vertex' vertices += Vertex)*
		'endloop'
	'endfacet'
;

Vertex returns Vertex:
	{Vertex}
	x=DOUBLE
	y=DOUBLE
	z=DOUBLE
;

terminal DOUBLE returns ecore::EDouble:
	(('-'|'+')? INT? '.' INT (('E'|'e') ('-'|'+')? INT)?) | INT;
	
EString returns ecore::EString:
	STRING | ID;
