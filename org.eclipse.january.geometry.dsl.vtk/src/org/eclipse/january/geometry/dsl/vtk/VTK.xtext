grammar org.eclipse.january.geometry.dsl.vtk.VTK with org.eclipse.xtext.common.Terminals hidden(SL_COMMENT, WS)

import "http://www.eclipse.org/january/geometry" 
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Geometry returns Geometry:
	{Geometry}
	name = EString
	(ENDLINE | (vertexSources += VertexSource) |
	('POLYGONS' INT INT ENDLINE?) (nodes += PolyShape)* |
	('TRIANGLE_STRIPS' INT INT ENDLINE?) (nodes+= TriangleStripPolyShape)*)*
;

VertexSource returns VertexSource:
	{VertexSource}
	('POINTS' EString? ENDLINE?)?
	(vertices += Vertex)+
;

EDouble returns ecore::EDouble:
	DOUBLE |  EInt
;

EInt returns ecore::EInt:
	'-'? INT
;

EString returns ecore::EString:
	(STRING | ID | INT | '.' | '/' | '\\' | ':' | '_' | '-' )+
;

Face returns Face:
	{Face}
	EInt (vertexIndices += EInt)+ ENDLINE?
;

PolyShape returns PolyShape:
	{PolyShape}
	(faces += Face)+
;

TriangleStripPolyShape returns TriangleStripPolyShape:
	{TriangleStripPolyShape}
	(faces += Face)+
;

Vertex returns Vertex:
	{Vertex}
	x = EDouble y = EDouble z = EDouble EInt? ENDLINE?
;

terminal DOUBLE returns ecore::EDouble: ('-'|'+')? INT? '.' INT (('E'|'e') ('-'|'+')? INT)?;

terminal ENDLINE: ('\r'? '\n' | '\r');

terminal SL_COMMENT: '#' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS: (' '|'\t' | 'ASCII' | 'DATASET POLYDATA' )+;